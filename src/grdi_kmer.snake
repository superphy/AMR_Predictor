
#################################################################

# Location of the raw genomes
RAW_GENOMES_PATH = "data/grdi_genomes/raw/"

# Kmer length that you want to count
KMER_SIZE = 11

# Data type of the resulting kmer matrix. Use uint8 if counts are
# all under 256. Else use uint16 (kmer counts under 65536)
MATRIX_DTYPE = 'uint8'

#################################################################

ids, = glob_wildcards(RAW_GENOMES_PATH+"{id}.fasta")

rule all:
  input:
    ".grdi_touchfile.txt"

rule clean:
  input:
    RAW_GENOMES_PATH+"{id}.fasta"
  output:
    "data/grdi_genomes/clean/{id}.fasta"
  run:
    shell("python src/clean.py {input} data/grdi_genomes/clean/")

rule count:
  input:
    "data/grdi_genomes/clean/{id}.fasta"
  output:
    temp("data/grdi_jellyfish_results/{id}.jf")
  threads:
    2
  shell:
    "jellyfish count -C -m {KMER_SIZE} -s 100M -t {threads} {input} -o {output}"

rule dump:
    input:
        "data/grdi_jellyfish_results/{id}.jf"
    output:
        "data/grdi_jellyfish_results/{id}.fa"
    shell:
        "jellyfish dump {input} > {output}"

rule matrix:
    input:
        expand("data/grdi_jellyfish_results/{id}.fa", id=ids)
    output:
        touch(".grdi_touchfile.txt")
    shell:
        "python src/parallel_matrix.py {KMER_SIZE} {MATRIX_DTYPE} data/grdi_jellyfish_results/ data/grdi_unfiltered/"
